AUCTIONHOUSE

network channel AUCTIONTIMER/BEGIN@{} : int@{};
network channel AUCTIONTIMER/EXIT@{} : int@{};

network channel ALICE/TO_LEAD@{H} $1 : int@{H};
network channel ALICE/AUCTION_OVER_NAME@{} : string@{H};
network channel ALICE/AUCTION_OVER_BID@{} : int@{H};

network channel BOB/TO_LEAD@{H} $1 : int@{H};
network channel BOB/AUCTION_OVER_NAME@{} : string@{H};
network channel BOB/AUCTION_OVER_BID@{} : int@{H};

var winner : string@{H} = "";
var winning_bid : int@{H} = 0;
var round_counter : int@{} = 500;

START@{} (i : int@{}) {
    send(ALICE/TO_LEAD, 1);
    send(BOB/TO_LEAD, 1);
    send(AUCTIONTIMER/BEGIN, 1);
}

ALICE_BID@{H} (bid: int@{H}) {
    oblif winning_bid < bid
    then {
        winner ?= "Alice";
        winning_bid ?= bid;
    }
    else skip;
}

BOB_BID@{H} (bid: int@{H}) {
    oblif winning_bid < bid
    then {
        winner ?= "Bob";
        winning_bid ?= bid;
    }
    else skip;
}

TICK@{} $4 (dmy: int@{}) {
    if round_counter > 0
    then {
        oblif winner != "Alice"
        then send(ALICE/TO_LEAD, winning_bid+1);
        else skip;

        oblif winner != "Bob"
        then send(BOB/TO_LEAD, winning_bid+1);
        else skip;

        round_counter = round_counter - 1;
        send(AUCTIONTIMER/BEGIN, 1);
    } else {
        send(ALICE/AUCTION_OVER_NAME,winner);
        send(ALICE/AUCTION_OVER_BID,winning_bid);

        send(BOB/AUCTION_OVER_NAME,winner);
        send(BOB/AUCTION_OVER_BID,winning_bid);

        send(AUCTIONTIMER/EXIT, 0);
        exit();
    }
}